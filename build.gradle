plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'groovy'
	id 'codenarc'
	id 'com.avast.gradle.docker-compose' version '0.10.9'
}

group = 'com.temp.aggregation'
version = '1.1.0'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

springBoot {
	buildInfo()
}

dependencies {
	codenarc 'org.codenarc:CodeNarc:1.1'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.2.6.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.2.6.RELEASE'
	implementation 'org.postgresql:postgresql:42.2.11'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.2.6.RELEASE'
	implementation 'org.codehaus.groovy:groovy-all:2.5.10'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3'
	implementation 'com.google.api-client:google-api-client:1.30.5'
	implementation 'net.logstash.logback:logstash-logback-encoder:5.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.6.RELEASE'
	testImplementation 'org.spockframework:spock-core:2.0-M2-groovy-2.5'
	testImplementation 'org.spockframework:spock-spring:2.0-M2-groovy-2.5'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign:2.1.3.RELEASE'
}

test {
	useJUnitPlatform()
	maxParallelForks = 1
}

bootJar {
  archiveName = 'kelvin-api.jar'
}

dockerCompose.isRequiredBy(test)
dockerCompose.isRequiredBy(bootRun)
dockerCompose {
	useComposeFiles = [rootProject.file('src/test/resources/compose/compose-test.yml').absolutePath]
	executable = '/usr/local/bin/docker-compose'
	dockerExecutable = '/usr/local/bin/docker'
}

codenarc {
	configFile rootProject.file('gradle/codenarc/codenarc.groovy')
	if (!System.getenv('GEN_CODENARC_REPORT')) {
		reportFormat = 'console'
	}
}

codenarcTest {
	configFile rootProject.file('gradle/codenarc/codenarcTest.groovy')
}

// Force compile static for all files
compileGroovy {
	configure(groovyOptions) {
		configurationScript = file("$rootDir/gradle/groovy/compiler-config.groovy")
	}
}
